%
% ECLiPSe Nonogram Solver
%
% by Chris Mears, Monash University
%
% Problem:
%
% Nonograms are a popular puzzle, which goes by different names in
% different countries.  The player has to shade in squares in a grid so
% that blocks of consecutive shaded squares satisfy constraints given
% for each row and column.  Constraints typically indicate the sequence
% of shaded blocks (e.g. [3,1,2] means that there is a block of 3, then
% a gap of unspecified size, a block of length 1, another gap, and then
% a block of length 2). Data for sample problems is at the end of this file,
%   
% Example run:
% 
% ?- go(n4).
%  * *       *
%            *
%      * *    
%      * *    
%  *          
%  *       * *
% Yes (0.01s cpu, solution 1, maybe more)
% No (0.01s cpu)
%

:- use_module(regular).	% from http://eclipseclp.org/examples/regular.ecl
:- lib(ic).

run_all :-
        data(Name, R, C, RB, CB),
        write(Name), write(": "), write(R), write("x"), writeln(C),
        check_data(R, C, RB, CB),
        statistics(times, [CPU0, _, _]),
        go(Name),
        statistics(times, [CPU1, _, _]),
        Time is CPU1-CPU0,
        write(Time), writeln(" seconds"), nl,
        fail.
run_all :- writeln('Finished!').

go(Name) :-
        data(Name, Rows, Columns, RowBlocks, ColumnBlocks, Blacks),
        dim(Board, [Rows,Columns]),
	( foreach([I,J],Blacks), param(Board) do
	    Board[I,J] #= 1
	),
        ( foreacharg(R, Board), foreach(RB, RowBlocks) do line(RB, R) ),
        m_transpose(Board, BoardT),
        ( foreacharg(C, BoardT), foreach(CB, ColumnBlocks) do line(CB, C) ),

        % Search.
        % It seems faster to label 1s before 0s, so we use indomain_max.
	search(Board, 0, input_order, indomain_max, complete, []),

        % Print the board.
        pretty_print(Board).

% Pretty print the board (taken from Joachim's example).
pretty_print(Board) :-
	dim(Board, [M,N]),
	( for(I,1,M), param(Board,N) do
	    ( for(J,1,N), param(Board,I) do
		X is Board[I,J],
		( X==0 -> write("  ")
		; X==1 -> write(" *")
		;         write(" ?")
		)
	    ), nl
	), nl.

% Matrix transpose.
m_transpose(A, T) :-
        dim(A, [R,C]),
        dim(T, [C,R]),
        ( foreachelem(X,A,[I,J]), param(T) do
            subscript(T, [J,I], X) ).

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Constrain a list of variables (board row/column) to contain the list
% of blocks.
%
% E.g. length(Xs, 15), line([3,2,3], Xs).
%
% Builds the transition function of a DFA and passes it to regular.

line(Blocks, Xs) :-
        ( fromto(Blocks, [Block|Brest], Brest, [BLast]),
          fromto(1, StateIn, StateOut, StateLast),
          fromto(Table0, TableOut, TableIn, []) do
            myblock(Block, StateIn, S0, TableIn, TableOut0),
            StateOut is S0+1,
            add([0, S0, StateOut], TableOut0, TableOut) ),
	myblock(BLast, StateLast, FinalState, Table0, Table1),
        add([0, FinalState, FinalState], Table1, Table),

        regular(table_predicate(Table), 1, [FinalState], Xs).

table_predicate(List, A, B, C) :-
        member([A,B,C], List).

myblock(Size, StateIn, StateOut, TableIn, TableOut) :-
        add([0, StateIn, StateIn], TableIn, Table0),
        ( for(_,1,Size),
          fromto(StateIn, SIn, SOut, StateOut),
          fromto(Table0, TIn, TOut, TableOut) do
            SOut is SIn+1,
            add([1, SIn, SOut], TIn, TOut) ).

add(X, Xs, [X|Xs]).

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

        
%----------------------------------------------------------------------
% sample problems
%
% data(ProblemName, NRows, NColumns, RowBlocks, ColumnBlocks, BlackFields)
%----------------------------------------------------------------------

% from http://www-lp.doc.ic.ac.uk/UserPages/staff/ft/alp/humour/visual/nono.html
data(ps, 9, 8,
    [[3],[2,1],[3,2],[2,2],[6],[1,5],[6],[1],[2]],	% row blocks
    [[1,2],[3,1],[1,5],[7,1],[5],[3],[4],[3]],		% column blocks
    []							% given black coordinates
).

% from http://www.pro.or.jp/~fuji/java/puzzle/nonogram/index-eng.html
data(n2, 10, 10,
    [[1],[3],[1,3],[2,4],[1,2],[2,1,1],[1,1,1,1],[2,1,1],[2,2],[5]],
    [[4],[1,3],[2,3],[1,2],[2,2],[1,1,1],[1,1,1,1],[1,1,1],[1,2],[5]],
    []
).
data(n3, 10, 15,
    [[4],[1,1,6],[1,1,6],[1,1,6],[4,9],[1,1],[1,1],[2,7,2],[1,1,1,1],[2,2]],
    [[4],[1,2],[1,1],[5,1],[1,2],[1,1],[5,1],[1,1],[4,1],[4,1],[4,2],[4,1],[4,1],[4,2],[4]],
    []
).
data(n4, 6, 6,
    [[2,1],[1],[2],[2],[1],[1,2]],
    [[1,2],[1],[2],[2],[1],[2,1]],
    []
).
data(n5, 10, 10,
    [[3],[3],[1],[3],[6],[3],[3],[3,3],[2,2],[2,1]],
    [[1],[1,2],[1,2],[1,1],[2,5],[7],[2,5],[1],[2],[2]],
    []
).
data(n6, 15, 15,
    [[5],[2,2],[1,1],[1,1],[4,4],[2,2,1,2],[1,3,1],[1,1,1,1],[2,7,2],[4,1,5],[2,1,1],[1,1,2],[1,1,1],[2,5,2],[3,4]],
    [[4],[2,2],[1,5],[1,2,2],[5,2,1],[2,1,1,2],[1,3,1],[1,1,6],[1,3,1],[2,1,2,2],[4,2,1],[1,1,1],[1,3,2],[2,2,3],[4]],
    []
).
data(n16, 15, 15,
    [[4],[2,2],[2,2],[2,4,2],[2,1,1,2],[2,4,2],[1,2],[4,4,4],[1,1,1,1,1,1],[4,1,1,4],[1,1,1],[1,1,3],[10],[2,1],[4,1]],
    [[5,1],[2,1,1,1],[2,1,1,2],[2,3,3],[2,1],[2,3,6],[1,1,1,1,1],[1,1,1,1,1],[2,3,6],[2,1],[2,3,1],[2,1,1,1],[2,1,1,4],[7],[1,1]],
    []
).
data(n19, R, C, RB, CB, Bs) :-
    data(p199, R, C, RB, CB, Bs).

% from http://www.puzzle.gr.jp/nonogram/prob/0200_e.html
data(p197, 20, 15,	% difficulty 7
    [[3],[1,2],[1,4],[1,1,2],[1,1,1,1],[1,3,2],[2,3,1],[1,1,1,2],[2,2,2],[1,1,2,2],[1,1,2,2],[1,1,1,1],[4,1,1],[2,2,2,1],[2,3,3],[2,2,3],[1,3,1,1],[2,1,1,1,2],[1,2,3],[1,6]],
    [[4,3],[6,1,2,3],[2,3],[6],[1,2,2],[1,1,2],[2,4,1,1],[1,1,2,2,2,1],[1,1,1,2,1,1],[1,3,2,3],[3,2,2],[4,3,4,2],[1,3,4,5],[2,2],[3]],
    []
).
data(p199, 20, 20,	% difficulty 8
    [[1,1,4],[1,6],[1,1,1,1,2,3],[1,1,2,3],[3,1,2,3],[4,5,2,2],[7,3,2],[3,5,1,2],[2,2,4,1],[2,2,3,4],[2,5,2],[2,1,5,1],[2,2,3,1],[6,2,2],[1,7],[2,2,2],[1,4],[3,1,1],[1,1],[1,1]],
    [[6,1],[8,3],[3,2,1],[1,1,2,2,1],[1,2,2,1,1],[1,1,1,1],[2,3],[4,1,2,2],[5,2,1],[8,1,1],[7,2],[3,5,2],[2,5],[2,1,4],[2,2,2,2],[2,2,1,1,1],[3,1,1,1,1],[5,4,2,1],[7,4,1,1],[4]],
    []
).
data(p200, 25, 25,	% difficulty 9
    [[1,1,2,2],[5,5,7],[5,2,2,9],[3,2,3,9],[1,1,3,2,7],[3,1,5],[7,1,1,1,3],[1,2,1,1,2,1],[4,2,4],[1,2,2,2],[4,6,2],[1,2,2,1],[3,3,2,1],[4,1,15],[1,1,1,3,1,1],[2,1,1,2,2,3],[1,4,4,1],[1,4,3,2],[1,1,2,2],[7,2,3,1,1],[2,1,1,1,5],[1,2,5],[1,1,1,3],[4,2,1],[3]],
    [[2,2,3],[4,1,1,1,4],[4,1,2,1,1],[4,1,1,1,1,1,1],[2,1,1,2,3,5],[1,1,1,1,2,1],[3,1,5,1,2],[3,2,2,1,2,2],[2,1,4,1,1,1,1],[2,2,1,2,1,2],[1,1,1,3,2,3],[1,1,2,7,3],[1,2,2,1,5],[3,2,2,1,2],[3,2,1,2],[5,1,2],[2,2,1,2],[4,2,1,2],[6,2,3,2],[7,4,3,2],[7,4,4],[7,1,4],[6,1,4],[4,2,2],[2,1]],
    []
).

% the example quoted in Optima#65, Mathematical Programming Society Newsletter
data(optima, 20, 20,
    [[7,1],[1,1,2],[2,1,2],[1,2,2],[4,2,3],[3,1,4],[3,1,3],[2,1,4],[2,9],[2,1,5],[2,7],[14],[8,2],[6,2,2],[2,8,1,3],[1,5,5,2],[1,3,2,4,1],[3,1,2,4,1],[1,1,3,1,3],[2,1,1,2]],
    [[1,1,1,2],[3,1,2,1,1],[1,4,2,1,1],[1,3,2,4],[1,4,6,1],[1,11,1],[5,1,6,2],[14],[7,2],[7,2],[6,1,1],[9,2],[3,1,1,1],[3,1,3],[2,1,3],[2,1,5],[3,2,2],[3,3,2],[2,3,2],[2,6]],
    []
).

data(cast, 60, 35,
    [[2,3,1,5,1,7,1], [2,4,2,3,2,3,5], [2,6,3,1,1,5,1,5], [2,4,2,1,1,1,4,1,1,2], [2,8,2,1,5,2,5],
	[3,1,6,2,5,1,5], [3,3,3,1,1,6,1,1,1], [3,2,2,2,2,8,1,1,3], [1,4,4,3,7,1,1], [1,2,2,2,3,7,9],
	[1,2,3,1,1,5,2,2], [2,2,3,1,1,6,1], [1,3,1,5,4,1], [1,3,1,1,6,1,3,1], [3,3,4,5,1,4,2,1],
	[2,3,3,9,7,1], [2,3,2,2,1,1,3,5], [4,2,1,1,1,1,2,3], [4,2,2,1,4,3,2], [4,3,16,2],
	[1,2,5,7,1], [4,3,2,2,7,1], [2,3,1,10,1], [2,4,2,1,4,1], [1,6,7,3,1], 
	[3,11,3,1], [7,1,11,2,1], [2,2,2,2,2,2,2], [3,1,1,1,1,2,1], [2,2,2,2,1,1,1], 
	[1,1,1,1,2,1,2], [2,2,2,2,1,1,1,1], [4,1,1,2,2], [5,2,17,2,1], [9,2,3,1,4,2], 
	[9,4,2,1,1,1], [5,4,2,1,4], [11,1,2,1,4,1,2], [3,4,2,4,4], [2,1,4,1,2,1,5,2], 
	[8,4,1,1,2], [1,1,3,2,3], [1,3,1,8,1,6], [2,1,7,14], [1,2,4,4,1,2,3], 
	[1,1,4,2,1,1,1,1,1,4], [3,5,3,1,1,4], [2,4,2,2,1,2], [4,2,3,8,4], [4,15,2,2,4], 
	[4,1,10,2,1,2], [2,12,6,1,2,4], [3,1,3,1,3,3,4], [3,1,2,3,4,1], [5,2,2,2,3,3,3], 
	[1,2,2,2,2,4,1,1,3], [2,1,4,2,7,1,1], [5,2,2,3,6,3], [3,3,2,2,3,2,3], [4,1,2,1,1,2,1]],
    [[12,1,1,1], [8,6,3,1,3], [5,8,4,3,1,5], [7,3,4,1,3,5,1,7], [2,2,4,9,1,5,1,1,1,1,1,1,1], 
	[4,5,10,2,1,8,7,1], [5,1,3,3,16,1,2], [8,5,1,2,4,9,1,3], [4,5,3,14,1,1,1,1,4,1,1,3], [3,3,2,2,2,4,1,1,1,1,1,1,1,1,3,1,1,3,2], 
	[8,2,7,2,1,1,2,1,1,3,3], [1,5,9,12,2,1,1,3,1,1,2,2,1], [3,2,2,1,1,1,1,4,1,1,1,3,3,1,1,2,2], [5,2,2,2,2,1,5,2,1,1,2,5], [3,5,9,2,1,1,6,3,1,3,2,3], 
	[1,4,1,1,1,4,1,5,5,3,3,3], [4,1,1,1,1,3,4,6,6,3], [3,1,3,1,1,3,3,1,1,4,6,1], [3,1,5,1,1,3,1,1,9,4,1], [2,1,1,7,1,4,1,1,1,1,1,1,3,5], 
	[9,2,1,3,1,1,1,1,4,2,1], [1,14,1,1,2,2,2,10,1,2], [1,9,2,1,2,6,1,5,3,2], [1,9,9,1,2,2,3,1,1,4,3,1], [10,1,3,4,1,3,2,1,2,8], 
	[9,1,3,5,1,1,1,2,7], [4,5,1,2,5,1,3,1,1,2,1,3], [1,1,1,1,2,6,2,3,2,1,1,2,3,1], [1,6,1,5,7,1,3,3,2,4,3], [1,2,1,2,9,1,5,2,6,2], 
	[10,2,2,13,1,3,3,1], [2,2,1,6,2,3,3,2,2,2,1], [2,2,1,1,12,2,2,9,2,2,2,2], [5,1,2,4,1,5,11,2,2], [15,6,18]],
    []
).
	
% from http://www.gchq.gov.uk/press_and_media/news_and_features/Pages/Directors-Christmas-puzzle-2015.aspx
data(gchq, 25, 25,
	[
	    [7,3,1,1,7],
	    [1,1,2,2,1,1],
	    [1,3,1,3,1,1,3,1],
	    [1,3,1,1,6,1,3,1],
	    [1,3,1,5,2,1,3,1],
	    [1,1,2,1,1],
	    [7,1,1,1,1,1,7],
	    [3,3],
	    [1,2,3,1,1,3,1,1,2],
	    [1,1,3,2,1,1],
	    [4,1,4,2,1,2],
	    [1,1,1,1,1,4,1,3],
	    [2,1,1,1,2,5],
	    [3,2,2,6,3,1],
	    [1,9,1,1,2,1],
	    [2,1,2,2,3,1],
	    [3,1,1,1,1,5,1],
	    [1,2,2,5],
	    [7,1,2,1,1,1,3],
	    [1,1,2,1,2,2,1],
	    [1,3,1,4,5,1],
	    [1,3,1,3,10,2],
	    [1,3,1,1,6,6],
	    [1,1,2,1,1,2],
	    [7,2,1,2,5]
	],
	[
	    [7,2,1,1,7],
	    [1,1,2,2,1,1],
	    [1,3,1,3,1,3,1,3,1],
	    [1,3,1,1,5,1,3,1],
	    [1,3,1,1,4,1,3,1],
	    [1,1,1,2,1,1],
	    [7,1,1,1,1,1,7],
	    [1,1,3],
	    [2,1,2,1,8,2,1],
	    [2,2,1,2,1,1,1,2],
	    [1,7,3,2,1],
	    [1,2,3,1,1,1,1,1],
	    [4,1,1,2,6],
	    [3,3,1,1,1,3,1],
	    [1,2,5,2,2],
	    [2,2,1,1,1,1,1,2,1],
	    [1,3,3,2,1,8,1],
	    [6,2,1],
	    [7,1,4,1,1,3],
	    [1,1,1,1,4],
	    [1,3,1,3,7,1],
	    [1,3,1,1,1,2,1,1,4],
	    [1,3,1,4,3,3],
	    [1,1,2,2,2,6,1],
	    [7,1,3,2,1,1]
	],
	[
	    [4,4],[4,5], [4,13],[4,14],[4,22],
	    [9,7],[9,8],[9,11],[9,15],[9,16],[9,19],
	    [17,7],[17,12],[17,17],[17,21],
	    [22,4],[22,5],[22,10],[22,11],[22,16],[22,21],[22,22]
	]).


% simple check for typos in the data

check_data(M, N, RowBlocks, ColBlocks) :-
	length(RowBlocks, M),
	length(ColBlocks, N),
	( foreach(Blocks,RowBlocks), fromto(0,S0,S1,RowTotal) do
	    S1 is S0+sum(Blocks)
	),
	( foreach(Blocks,ColBlocks), fromto(0,S0,S1,ColTotal) do
	    S1 is S0+sum(Blocks)
	),
	RowTotal = ColTotal,
	!.
check_data(_,_,_,_) :-
	writeln("Inconsistent input data!"),
	abort.
